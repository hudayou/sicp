(define (make-accumulator init)
  (let ((sum init))
    (lambda (n)
      (set! sum (+ sum n))
      sum)))
(define a (make-accumulator 5))
(a 10)
(a 10)

(define (make-monitored f)
  (let ((counter 0))
    (lambda (a)
      (cond ((eq? a 'how-many-calls?) counter)
            ((eq? a 'reset-count)
             (set! counter 0)
             counter)
            (else
              (set! counter (+ counter 1))
              (f a))))))
(define s (make-monitored sqrt))
(s 100)
(s 'how-many-calls?)

(define (make-account balance saved-password)
  (define (withdraw amount)
    (if (>= balance amount)
      (begin (set! balance (- balance amount))
             balance)
      "Insufficient funds"))
  (define (deposit amount)
    (set! balance (+ balance amount))
    balance)
  (define (call-the-cops)
    (lambda (m)
      "dial 911"))
  (let ((counter 0))
    (define (dispatch password m)
      (if (equal? password saved-password)
        (begin
          (set! counter 0)
          (cond ((eq? m 'withdraw) withdraw)
                ((eq? m 'deposit) deposit)
                (else (error "unknown request -- make-account"
                             m))))
        (begin
          (set! counter (+ counter 1))
          (if (> counter 7)
            (call-the-cops)
            (lambda (m)
              "incorrect password")))))
    dispatch))

(define acc (make-account 100 'secret-password))
(display ((acc 'secret-password 'withdraw) 40))
(newline)
(display ((acc 'some-other-password 'deposit) 50))
(newline)
(define (acc-n n)
  (let loop ((i 1))
    (if (>= i n)
      ((acc 'some-other-password 'deposit) 50)
      (begin
        ((acc 'some-other-password 'deposit) 50)
        (loop (+ i 1))))))
